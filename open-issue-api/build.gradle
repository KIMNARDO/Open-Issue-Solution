plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
}

group = 'com.papsnet'
version = '1.0.0'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'

    // Swagger/OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

    // SQL Server JDBC Driver
    implementation 'com.microsoft.sqlserver:mssql-jdbc:12.4.2.jre11'

    // SAP JCO
    implementation files('lib/sapjco3.jar')

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Apache Commons
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-collections:commons-collections:3.2.2'

    // Logging
    implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'

    // YAML Resource
    implementation 'dev.akkinoc.util:yaml-resource-bundle:2.9.1'

    // JSON
    implementation 'org.json:json:20230227'

    // Apache POI (Excel)
    implementation 'org.apache.poi:poi:5.2.5'
    implementation 'org.apache.poi:poi-ooxml:5.2.5'

    // HTTP Client
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'

    // iText PDF
    implementation 'com.itextpdf:html2pdf:5.0.3'
    implementation 'com.itextpdf:layout:8.0.3'
    implementation 'com.itextpdf:forms:8.0.4'
    implementation 'com.itextpdf:svg:8.0.3'
    implementation 'com.itextpdf:sign:8.0.3'
    implementation 'com.itextpdf:styled-xml-parser:8.0.3'
    implementation 'com.itextpdf:io:8.0.3'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Dev Tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.2'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Profile 설정 (기본값: local)
ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

println "Current profile: ${profile}"

// Profile에 따른 리소스 설정
sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources", "src/main/resources-env/${profile}"]
        }
    }
}

tasks {
    processResources {
        duplicatesStrategy = org.gradle.api.file.DuplicatesStrategy.INCLUDE
    }
}
